<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<HTML>
 <HEAD>
   <TITLE> [Haiku-commits] r23109 - in	haiku/branches/developer/bonefish/optimization:	headers/private/kernel/arch/x86 src/system/kernel/arch/x86
   </TITLE>
   <LINK REL="Index" HREF="http://lists.berlios.de/pipermail/haiku-commits/2007-December/index.html" >
   <LINK REL="made" HREF="mailto:haiku-commits%40lists.berlios.de?Subject=Re%3A%20%5BHaiku-commits%5D%20r23109%20-%20in%0A%09haiku/branches/developer/bonefish/optimization%3A%0A%09headers/private/kernel/arch/x86%20src/system/kernel/arch/x86&In-Reply-To=%3C200712110006.lBB06MlK020890%40sheep.berlios.de%3E">
   <META NAME="robots" CONTENT="index,nofollow">
   <style type="text/css">
       pre {
           white-space: pre-wrap;       /* css-2.1, curent FF, Opera, Safari */
           }
   </style>
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="005009.html">
   <LINK REL="Next"  HREF="005025.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[Haiku-commits] r23109 - in	haiku/branches/developer/bonefish/optimization:	headers/private/kernel/arch/x86 src/system/kernel/arch/x86</H1>
    <B>bonefish at BerliOS</B> 
    <A HREF="mailto:haiku-commits%40lists.berlios.de?Subject=Re%3A%20%5BHaiku-commits%5D%20r23109%20-%20in%0A%09haiku/branches/developer/bonefish/optimization%3A%0A%09headers/private/kernel/arch/x86%20src/system/kernel/arch/x86&In-Reply-To=%3C200712110006.lBB06MlK020890%40sheep.berlios.de%3E"
       TITLE="[Haiku-commits] r23109 - in	haiku/branches/developer/bonefish/optimization:	headers/private/kernel/arch/x86 src/system/kernel/arch/x86">bonefish at mail.berlios.de
       </A><BR>
    <I>Tue Dec 11 01:06:22 CET 2007</I>
    <P><UL>
        <LI>Previous message: <A HREF="005009.html">[Haiku-commits] r23108 - in	haiku/branches/developer/bonefish/optimization:	headers/private/kernel src/system/kernel
</A></li>
        <LI>Next message: <A HREF="005025.html">[Haiku-commits] r23109 - in	haiku/branches/developer/bonefish/optimization:	headers/private/kernel/arch/x86 src/system/kernel/arch/x86
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#5010">[ date ]</a>
              <a href="thread.html#5010">[ thread ]</a>
              <a href="subject.html#5010">[ subject ]</a>
              <a href="author.html#5010">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>Author: bonefish
Date: 2007-12-11 01:06:20 +0100 (Tue, 11 Dec 2007)
New Revision: 23109
ViewCVS: <A HREF="http://svn.berlios.de/viewcvs/haiku?rev=23109&amp;view=rev">http://svn.berlios.de/viewcvs/haiku?rev=23109&amp;view=rev</A>

Modified:
   haiku/branches/developer/bonefish/optimization/headers/private/kernel/arch/x86/arch_cpu.h
   haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/arch_interrupts.S
   haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/asm_offsets.cpp
   haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/commpage.c
   haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/syscall.S
Log:
Added Intel sysenter/sysexit support (commpage, handler). It seems to
basically work, but ATM we trash edx on return, which is one half of
the return value for syscalls returning 64 bit values.


Modified: haiku/branches/developer/bonefish/optimization/headers/private/kernel/arch/x86/arch_cpu.h
===================================================================
--- haiku/branches/developer/bonefish/optimization/headers/private/kernel/arch/x86/arch_cpu.h	2007-12-10 23:50:40 UTC (rev 23108)
+++ haiku/branches/developer/bonefish/optimization/headers/private/kernel/arch/x86/arch_cpu.h	2007-12-11 00:06:20 UTC (rev 23109)
@@ -21,6 +21,9 @@
 #define IA32_MSR_APIC_BASE				0x1b
 
 #define IA32_MSR_MTRR_CAPABILITIES		0xfe
+#define IA32_MSR_SYSENTER_CS			0x174
+#define IA32_MSR_SYSENTER_ESP			0x175
+#define IA32_MSR_SYSENTER_EIP			0x176
 #define IA32_MSR_MTRR_DEFAULT_TYPE		0x2ff
 #define IA32_MSR_MTRR_PHYSICAL_BASE_0	0x200
 #define IA32_MSR_MTRR_PHYSICAL_MASK_0	0x201

Modified: haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/arch_interrupts.S
===================================================================
--- haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/arch_interrupts.S	2007-12-10 23:50:40 UTC (rev 23108)
+++ haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/arch_interrupts.S	2007-12-11 00:06:20 UTC (rev 23109)
@@ -8,9 +8,10 @@
  */
 
 #include &lt;thread_types.h&gt;
-#include &lt;arch/x86/descriptors.h&gt;
 #include &lt;arch/x86/arch_cpu.h&gt;
 #include &lt;arch/x86/arch_kernel.h&gt;
+#include &lt;arch/x86/commpage.h&gt;
+#include &lt;arch/x86/descriptors.h&gt;
 
 #include &quot;asm_offsets.h&quot;
 #include &quot;syscall_numbers.h&quot;
@@ -65,6 +66,21 @@
 	/* thread-&gt;in_kernel = false; */						\
 	movb	$0, THREAD_in_kernel(%edi)
 
+#define PUSH_IFRAME_BOTTOM(iframeType)	\
+	pusha;								\
+	push	%ds;						\
+	push	%es;						\
+	push	%fs;						\
+	push	%gs;						\
+	pushl	$iframeType
+
+#define PUSH_IFRAME_BOTTOM_SYSCALL()	\
+	pushl	$0;							\
+	pushl	$99;						\
+	pushl	%edx;						\
+	pushl	%eax;						\
+	PUSH_IFRAME_BOTTOM(IFRAME_TYPE_SYSCALL)
+
 #define POP_IFRAME_AND_RETURN()										\
 	/* skip iframe type */											\
 	lea		4(%ebp), %esp;											\
@@ -80,7 +96,10 @@
 						   original eax/edx values */				\
 	iret
 
+#define SYSENTER_RETURN_ADDRESS \
+	(USER_COMMPAGE_ADDR + TABLE_ENTRIES * 4 + 4)
 
+
 .text
 
 #define TRAP_ERRC(name, vector) \
@@ -164,18 +183,16 @@
 .align 16
 .globl int_bottom
 int_bottom:
-	pusha
-	push	%ds
-	push	%es
-	push	%fs
-	push	%gs
-	push	$IFRAME_TYPE_OTHER
+	PUSH_IFRAME_BOTTOM(IFRAME_TYPE_OTHER)
 
 	movl	%esp, %ebp		// frame pointer is the iframe
 
 	cmp		$USER_CODE_SEG, IFRAME_cs(%ebp)
 	je		int_bottom_user
 
+	// disable interrupts -- the handler will enable them, if necessary
+	cli
+
 	pushl	%ebp
 	movl	IFRAME_vector(%ebp), %eax
 	call	*gInterruptHandlerTable(, %eax, 4)
@@ -230,16 +247,7 @@
 .globl trap99
 trap99:
 	// push error, vector, orig_edx, orig_eax, and other registers
-	pushl	$0
-	pushl	$99
-	pushl	%edx
-	pushl	%eax
-	pusha
-	push	%ds
-	push	%es
-	push	%fs
-	push	%gs
-	push	$IFRAME_TYPE_SYSCALL
+	PUSH_IFRAME_BOTTOM_SYSCALL()
 
 	// save %eax, the number of the syscall
 	movl	%eax, %esi
@@ -364,8 +372,120 @@
 	call	thread_at_kernel_exit
 	cli								// disable interrupts
 	jmp		kernel_exit_work_done
-	
 
+
+/*!	Handler called by the sysenter instruction
+	ecx - user esp
+*/
+FUNCTION(x86_sysenter):
+	// switch the stack
+	movl	%dr3, %edx
+	movl	THREAD_kernel_stack_top(%edx), %esp
+
+	// push the iframe
+	pushl	$USER_DATA_SEG			// user_ss
+	pushl	%ecx					// user_esp
+	pushfl							// eflags
+	orl		$(1 &lt;&lt; 9), (%esp)		// set the IF (interrupts) bit
+	pushl	$USER_CODE_SEG			// user cs
+	pushl	$SYSENTER_RETURN_ADDRESS	// user_eip
+
+	PUSH_IFRAME_BOTTOM_SYSCALL()
+
+	// save %eax, the number of the syscall
+	movl	%eax, %esi
+
+	movl	$KERNEL_DATA_SEG,%eax
+	cld
+	movl	%eax,%ds
+	movl	%eax,%es
+
+	movl	%esp, %ebp				// frame pointer is the iframe
+
+	// disable breakpoints, if installed
+	movl	%dr3, %edi				// thread pointer
+	testl	$THREAD_FLAGS_BREAKPOINTS_INSTALLED, THREAD_flags(%edi)
+	jz		skip_debug_at_kernel_entry_sysenter
+	call	x86_exit_user_debug_at_kernel_entry
+  skip_debug_at_kernel_entry_sysenter:
+
+	// update the thread's user time
+	UPDATE_THREAD_USER_TIME_PUSH_TIME()
+		// leave the time on the stack (needed for post syscall debugging)
+
+	sti								// enable interrupts
+
+	cmp		$SYSCALL_COUNT, %esi	// check syscall number
+	jae		bad_syscall_number
+	movl	$kSyscallInfos, %eax	// get syscall info
+	lea		(%eax, %esi, SYSCALL_INFO_sizeof), %esi
+
+	// copy parameters onto this stack
+	subl	$80, %esp				// leave some room for the syscall params
+
+	// get the address of the syscall parameters
+	movl	IFRAME_user_esp(%ebp), %eax
+	addl	$4, %eax				
+	cmp		$KERNEL_BASE, %eax		// must not be a kernel address
+	jae		bad_syscall_params
+
+	// get and push the syscall parameter size
+	movl	SYSCALL_INFO_parameter_size(%esi), %ebx
+	push	%ebx
+
+	push	%eax
+	lea		8(%esp), %ebx			// stack address where to copy the params
+	push	%ebx
+	call	user_memcpy
+	addl	$12, %esp
+	test	%eax, %eax
+	jne		bad_syscall_params
+
+	// pre syscall debugging
+	testl	$THREAD_FLAGS_DEBUGGER_INSTALLED, THREAD_flags(%edi)
+	jnz		do_pre_syscall_debug
+		// if debugging is enabled, we take the slow syscall exit
+
+	// call the syscall function
+	call	*SYSCALL_INFO_function(%esi)
+
+	// overwrite the values of %eax and %edx on the stack (the syscall return
+	// value)
+	movl	%edx, IFRAME_edx(%ebp)
+	movl	%eax, IFRAME_eax(%ebp)
+
+	testl	$(THREAD_FLAGS_DEBUGGER_INSTALLED | THREAD_FLAGS_SIGNALS_PENDING \
+			| THREAD_FLAGS_DEBUG_THREAD | THREAD_FLAGS_BREAKPOINTS_DEFINED) \
+			, THREAD_flags(%edi)
+	jnz		post_syscall_work
+		// if any special work has to be done, we take the slow syscall exit
+
+	cli								// disable interrupts
+
+	// update the thread's kernel time
+	UPDATE_THREAD_KERNEL_TIME()
+
+	// pop the bottom of the iframe
+	lea		4(%ebp), %esp	// skip iframe type
+
+	pop		%gs
+	addl	$4, %esp	/* we skip %fs, as this contains the CPU
+						   dependent TLS segment */
+	pop		%es
+	pop		%ds
+
+	popa
+
+	// pop eflags, which also reenables interrupts
+	addl	$24, %esp	// skip, orig_eax/edx, vector, error_code, eip, cs
+	popfl
+
+	// ecx already contains the user esp -- load edx with the return address
+	movl	$SYSENTER_RETURN_ADDRESS, %edx
+
+	sysexit
+
+
 /*!	Is copied to the signal stack call to restore the original frame when
 	the signal handler exits.
 	The copying code (in arch_thread.c::arch_setup_signal_frame()) copies

Modified: haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/asm_offsets.cpp
===================================================================
--- haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/asm_offsets.cpp	2007-12-10 23:50:40 UTC (rev 23108)
+++ haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/asm_offsets.cpp	2007-12-11 00:06:20 UTC (rev 23109)
@@ -32,6 +32,7 @@
 	DEFINE_OFFSET_MACRO(THREAD, thread, last_time);
 	DEFINE_OFFSET_MACRO(THREAD, thread, in_kernel);
 	DEFINE_OFFSET_MACRO(THREAD, thread, flags);
+	DEFINE_OFFSET_MACRO(THREAD, thread, kernel_stack_top);
 
 	// struct iframe
 	DEFINE_OFFSET_MACRO(IFRAME, iframe, cs);
@@ -39,6 +40,8 @@
 	DEFINE_OFFSET_MACRO(IFRAME, iframe, edx);
 	DEFINE_OFFSET_MACRO(IFRAME, iframe, orig_eax);
 	DEFINE_OFFSET_MACRO(IFRAME, iframe, vector);
+	DEFINE_OFFSET_MACRO(IFRAME, iframe, eip);
+	DEFINE_OFFSET_MACRO(IFRAME, iframe, flags);
 	DEFINE_OFFSET_MACRO(IFRAME, iframe, user_esp);
 
 	// struct syscall_info

Modified: haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/commpage.c
===================================================================
--- haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/commpage.c	2007-12-10 23:50:40 UTC (rev 23108)
+++ haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/commpage.c	2007-12-11 00:06:20 UTC (rev 23109)
@@ -10,6 +10,8 @@
 
 #include &lt;KernelExport.h&gt;
 
+#include &lt;cpu.h&gt;
+#include &lt;smp.h&gt;
 #include &lt;vm.h&gt;
 #include &lt;vm_types.h&gt;
 
@@ -20,11 +22,16 @@
 static unsigned long *user_comm_ptr;
 static void *next_comm_addr;
 
-// user syscall assembly stub
+// user syscall assembly stubs
 extern void _user_syscall_int(void);
 extern unsigned int _user_syscall_int_end;
+extern void _user_syscall_sysenter(void);
+extern unsigned int _user_syscall_sysenter_end;
 
+// sysenter handler
+extern void x86_sysenter();
 
+
 static inline addr_t
 commpage_ptr_to_user_ptr(const void *ptr)
 {
@@ -32,15 +39,72 @@
 }
 
 
+static bool
+all_cpus_have_feature(enum x86_feature_type type, int feature)
+{
+	int i;
+	int cpuCount = smp_get_num_cpus();
+
+	for (i = 0; i &lt; cpuCount; i++) {
+		if (!(gCPU[i].arch.feature[type] &amp; feature))
+			return false;
+	}
+
+	return true;
+}
+
+
+static void
+init_intel_syscall_registers(void* dummy, int cpuNum)
+{
+	x86_write_msr(IA32_MSR_SYSENTER_CS, KERNEL_CODE_SEG);
+	x86_write_msr(IA32_MSR_SYSENTER_ESP, 0);
+	x86_write_msr(IA32_MSR_SYSENTER_EIP, (addr_t)x86_sysenter);
+}
+
+
+#if 0
+static void
+init_amd_syscall_registers(void* dummy, int cpuNum)
+{
+	// TODO: ...
+}
+#endif
+
+
 static status_t
 initialize_commpage_syscall(void)
 {
 	size_t len;
-	
-	// for now, we're hard coded to use the legacy method (int 99)
-	len = (size_t)((void *)&amp;_user_syscall_int_end - (void *)&amp;_user_syscall_int);
-	memcpy(next_comm_addr, &amp;_user_syscall_int, len);
+	void* syscallCode = &amp;_user_syscall_int;
+	void* syscallCodeEnd = &amp;_user_syscall_int_end;
 
+	// check syscall
+	if (all_cpus_have_feature(FEATURE_COMMON, IA32_FEATURE_SEP)
+		&amp;&amp; !(gCPU[0].arch.family == 6 &amp;&amp; gCPU[0].arch.model &lt; 3
+			&amp;&amp; gCPU[0].arch.stepping &lt; 3)) {
+		// Intel sysenter/sysexit
+		dprintf(&quot;initialize_commpage_syscall(): sysenter/sysexit supported\n&quot;);
+
+		// the code to be used in userland
+		syscallCode = &amp;_user_syscall_sysenter;
+		syscallCodeEnd = &amp;_user_syscall_sysenter_end;
+
+		// tell all CPUs to init their sysenter/sysexit related registers
+		call_all_cpus_sync(&amp;init_intel_syscall_registers, NULL);
+	} else if (all_cpus_have_feature(FEATURE_EXT_AMD,
+			IA32_FEATURE_AMD_EXT_SYSCALL)) {
+		// AMD syscall/sysret
+		dprintf(&quot;initialize_commpage_syscall(): syscall/sysret supported &quot;
+			&quot;-- not yet by Haiku, though&quot;);
+	} else {
+		// no special syscall support
+		dprintf(&quot;initialize_commpage_syscall(): no special syscall support\n&quot;);
+	}
+
+	len = (size_t)(syscallCodeEnd - syscallCode);
+	memcpy(next_comm_addr, syscallCode, len);
+
 	// fill in the table entry
 	comm_ptr[COMMPAGE_ENTRY_SYSCALL] = commpage_ptr_to_user_ptr(next_comm_addr);
 	next_comm_addr = (void *)((addr_t)next_comm_addr + ROUNDUP(len, 4));

Modified: haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/syscall.S
===================================================================
--- haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/syscall.S	2007-12-10 23:50:40 UTC (rev 23108)
+++ haiku/branches/developer/bonefish/optimization/src/system/kernel/arch/x86/syscall.S	2007-12-11 00:06:20 UTC (rev 23109)
@@ -9,11 +9,21 @@
 .text
 
 /* user space half of the syscall mechanism, to be copied into the commpage */
+
+// int 99 fallback
 FUNCTION(_user_syscall_int):
 	int	$99
 	ret
 SYM(_user_syscall_int_end):
 
 
+// Intel sysenter/sysexit
+FUNCTION(_user_syscall_sysenter):
+	// sysexit forces us to trash edx (-&gt; eip) and ecx (-&gt; esp), but they are
+	// scratch registers anyway. We use ecx right away to store esp.
+	movl	%esp, %ecx
+	sysenter
+	ret
+SYM(_user_syscall_sysenter_end):
 
 


</PRE>

<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="005009.html">[Haiku-commits] r23108 - in	haiku/branches/developer/bonefish/optimization:	headers/private/kernel src/system/kernel
</A></li>
	<LI>Next message: <A HREF="005025.html">[Haiku-commits] r23109 - in	haiku/branches/developer/bonefish/optimization:	headers/private/kernel/arch/x86 src/system/kernel/arch/x86
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#5010">[ date ]</a>
              <a href="thread.html#5010">[ thread ]</a>
              <a href="subject.html#5010">[ subject ]</a>
              <a href="author.html#5010">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="https://lists.berlios.de/mailman/listinfo/haiku-commits">More information about the Haiku-commits
mailing list</a><br>
</body></html>
